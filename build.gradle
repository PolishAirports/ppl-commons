plugins {
    id 'com.jfrog.bintray' version '1.3.1'
    id 'pl.allegro.tech.build.axion-release' version '1.3.2'
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven-publish'

scmVersion {
    ignoreUncommittedChanges = false

    tag {
        prefix = ''
    }
}

group = 'pl.ppl'
version = scmVersion.version

sourceCompatibility = '1.8'

ext {
    spockVersion = '1.0-groovy-2.4'
    springBootVersion = '1.5.2.RELEASE'
}

dependencies {
    compile 'org.hibernate.javax.persistence:hibernate-jpa-2.1-api:1.0.0.Final'
    compile 'org.springframework.data:spring-data-jpa:1.11.1.RELEASE'

    testCompile 'com.h2database:h2:1.4.187'
    testCompile 'org.codehaus.groovy:groovy-all:2.4.4'
    testCompile "org.spockframework:spock-core:$spockVersion"
    testRuntime "org.spockframework:spock-spring:$spockVersion"
    testCompile "org.springframework.boot:spring-boot-starter-data-jpa:$springBootVersion"
    testCompile "org.springframework.boot:spring-boot-starter-test:$springBootVersion"
}

repositories {
    mavenLocal()
    jcenter()
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['mavenJava']
    pkg {
        repo = 'ppl-commons'
        name = project.name
        userOrg = 'ppl-os'
        websiteUrl = 'https://github.com/PolishAirports/ppl-commons'
        issueTrackerUrl = 'https://github.com/PolishAirports/ppl-commons/issues'
        vcsUrl = 'https://github.com/PolishAirports/ppl-commons.git'
        licenses = ['Apache-2.0']
    }
}

bintrayUpload.dependsOn 'build'
